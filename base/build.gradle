apply plugin: 'com.android.feature'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

Properties props = new Properties()
props.load(new FileInputStream(file(project.property("austinfeedsme.properties"))))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion '28.0.3'
    baseFeature true

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        defaultConfig {
            versionCode 25
            versionName "1.7"
        }

        manifestPlaceholders = [google_maps_key: props['google_maps_key'],
                                fabricApiKey: props['fabric']]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "mode"

    productFlavors {
        mock {
            dimension "mode"
        }
        prod {
            dimension "mode"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name == 'release'
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true)
        }
    }
}

crashlytics { instantAppSupport true }

dependencies {
    application project(":apk")

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Support Libs
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha01'

    // Dagger 2
    implementation "com.google.dagger:dagger:$rootProject.ext.daggerVersion"
    implementation "com.google.dagger:dagger-android:$rootProject.ext.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.ext.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.ext.daggerVersion"

    // Firebase dependencies
    implementation "com.google.firebase:firebase-firestore:$rootProject.ext.firestoreVersion"
    implementation "com.google.firebase:firebase-auth:$rootProject.ext.firebaseVersion"
    implementation "com.google.firebase:firebase-core:$rootProject.ext.firebaseVersion"
    implementation 'com.google.firebase:firebase-messaging:17.3.4'
    implementation 'com.firebaseui:firebase-ui-auth:4.2.1'

    // Needed for Authentication
    implementation "com.google.android.gms:play-services-auth:$rootProject.ext.playServicesVersion"
    // Needed for MapView
    implementation "com.google.android.gms:play-services-maps:$rootProject.ext.playServicesVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.ext.retrofitVersion"

    // Image Loading
    implementation "com.github.bumptech.glide:glide:$rootProject.ext.glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.ext.glideVersion"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.0'

    // Debugging
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    // Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.3@aar') {
        transitive = true
    }

    // Unit testing dependencies
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-all:1.10.19"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "org.powermock:powermock-module-junit4:1.6.2"
    testImplementation "org.powermock:powermock-api-mockito:1.6.2"
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.8.1'

    // Espresso Dependencies
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.1.0', {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    })
}

// Needed for Firebase
apply plugin: 'com.google.gms.google-services'